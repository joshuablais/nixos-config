{ config, pkgs, ... }:
{
  home.activation = {
    # Repository cloning
    cloneRepos = config.lib.dag.entryAfter [ "writeBoundary" ] ''
      if ${pkgs.openssh}/bin/ssh -T git@github.com 2>&1 | grep -q "successfully authenticated"; then
        if [ ! -d "${config.home.homeDirectory}/.config/scripts" ]; then
          echo "Cloning private scripts repo..."
          ${pkgs.git}/bin/git clone git@github.com:joshuablais/scripts.git \
            ${config.home.homeDirectory}/.config/scripts
        fi
      else
        echo "SSH key not configured for GitHub, skipping private scripts repo"
      fi

      if [ ! -d "${config.home.homeDirectory}/Pictures/Wallpapers" ]; then
        ${pkgs.git}/bin/git clone https://github.com/joshuablais/Wallpapers \
          ${config.home.homeDirectory}/Pictures/Wallpapers
      fi

      if [ ! -d "${config.home.homeDirectory}/.config/kmonad" ]; then
        ${pkgs.git}/bin/git clone https://github.com/joshuablais/Kmonad-thinkpad \
          ${config.home.homeDirectory}/.config/kmonad
      fi
    '';

    # Doom sync
    syncDoomEmacs = config.lib.dag.entryAfter [ "linkGeneration" "installDoomEmacs" ] ''
      if [ -d "${config.home.homeDirectory}/.emacs.d" ] && \
         [ -d "${config.home.homeDirectory}/.config/doom" ]; then
        if [ -x "${config.home.homeDirectory}/.emacs.d/bin/doom" ]; then
          echo "Syncing Doom configuration..."
          export PATH="${pkgs.emacs}/bin:${pkgs.git}/bin:${pkgs.ripgrep}/bin:${pkgs.fd}/bin:$PATH"
          ${config.home.homeDirectory}/.emacs.d/bin/doom sync
        else
          echo "Warning: doom binary not found, skipping sync"
        fi
      else
        echo "Doom or doom config not found, skipping sync"
      fi
    '';
  };
}
